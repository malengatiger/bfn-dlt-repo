buildscript {
    ext {
        corda_release_group = 'net.corda'
        corda_release_version = '4.1'
        corda_gradle_plugins_version = '4.0.45'
        junit_version = '4.12'
        quasar_version = '0.7.11_r3'
        spring_boot_version = '2.0.4.RELEASE'
        spring_boot_gradle_plugin_version = '2.0.4.RELEASE'
        slf4j_version = '1.7.25'
        log4j_version = '2.11.2'

        corda_platform_version = '4'
        corbeans_version = '0.26'
        corda_solutions_version = '2.0'

        tokens_release_version = '1.0'
        tokens_release_group = 'com.r3.corda.lib.tokens'

        accounts_release_group = 'com.r3.corda.lib.accounts'
        accounts_release_version = '1.0-RC01'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda' }
        maven { url 'http://ci-artifactory.corda.r3cev.com/artifactory/corda-lib-dev' }
        maven { url 'http://ci-artifactory.corda.r3cev.com/artifactory/corda-lib' }
    }

    dependencies {
        classpath "net.corda.plugins:cordapp:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:cordformation:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:quasar-utils:$corda_gradle_plugins_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_gradle_plugin_version"
    }
}

allprojects {
    apply plugin: 'java'

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda' }
        maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-lib' }
        maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-lib-dev' }
        maven { url 'https://jitpack.io' }
        maven { url "https://repo.gradle.org/gradle/libs-releases-local" }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-parameters" // Required by Corda's serialisation framework.
    }

    jar {
        // This makes the JAR's SHA-256 hash repeatable.
        preserveFileTimestamps = false
        reproducibleFileOrder = true
        // Don't include log config files.
        exclude "**/log4j2*.xml"
        // We need this because log4j2.xml is pulled in from multiple dependency JARs.
        // JAR signing will fail if there are multiple files with the same name.
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
}


apply plugin: 'net.corda.plugins.cordapp'
apply plugin: 'net.corda.plugins.cordformation'
apply plugin: 'net.corda.plugins.quasar-utils'

cordapp {
    info {
        name "Business Finance Network CorDapp"
        vendor "OneConnect"
        targetPlatformVersion corda_platform_version.toInteger()
        minimumPlatformVersion corda_platform_version.toInteger()
    }
}

sourceSets {
    main {
        resources {
            srcDir rootProject.file("config/dev")
        }
    }
}

dependencies {
    testCompile "junit:junit:$junit_version"
    // For logging.
    testCompile "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_version}"
    testCompile "org.apache.logging.log4j:log4j-web:${log4j_version}"
    // Corda dependencies.
    cordaCompile "$corda_release_group:corda-core:$corda_release_version"
    cordaCompile "$corda_release_group:corda-node-api:$corda_release_version"
    cordaRuntime "$corda_release_group:corda:$corda_release_version"

    // CorDapp dependencies.
    cordapp project(":workflows")
    cordapp project(":contracts")

    // Accounts Library dependencies
    cordapp "$accounts_release_group:accounts-contracts:$accounts_release_version"
    cordapp "$accounts_release_group:accounts-flows:$accounts_release_version"

    // Token SDK dependencies.
    cordapp "$tokens_release_group:tokens-contracts:$tokens_release_version"
    cordapp "$tokens_release_group:tokens-workflows:$tokens_release_version"
    cordapp "$tokens_release_group:tokens-money:$tokens_release_version"

    cordaCompile "$accounts_release_group:accounts-contracts:$accounts_release_version"
    cordaCompile "$accounts_release_group:accounts-flows:$accounts_release_version"


    // Logging
    cordaCompile "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_version}"
    cordaCompile "org.apache.logging.log4j:log4j-web:${log4j_version}"
    cordaCompile "org.slf4j:jul-to-slf4j:$slf4j_version"

}

task deployNodes(type: net.corda.plugins.Cordform, dependsOn: ['jar']) {
    nodeDefaults {
        projectCordapp {
            deploy = false
        }
        cordapp project(':contracts')
        cordapp project(':workflows')

        cordapp("$tokens_release_group:tokens-contracts:$tokens_release_version")
        cordapp("$tokens_release_group:tokens-workflows:$tokens_release_version")
        cordapp("$tokens_release_group:tokens-money:$tokens_release_version")

        cordapp("$accounts_release_group:accounts-contracts:$accounts_release_version")
        cordapp("$accounts_release_group:accounts-flows:$accounts_release_version")
    }
    node {
        name "O=Notary,L=Pretoria,C=ZA"
        notary = [validating : false]
        p2pPort 10400
        rpcSettings {
            address("localhost:10401")
            adminAddress("localhost:10402")
        }
    }

    node {
        name "O=OCTNetworkOperator,L=Sandton,C=ZA"
        p2pPort 10403
        rpcSettings {
            address("localhost:10421")
            adminAddress("localhost:10422")
        }
        rpcUsers = [[ user: "user1", "password": "test", "permissions": ["ALL"]]]

    }
//    node {
//        name "O=LondonInvestor,L=London,C=GB"
//        p2pPort 10404
//        rpcSettings {
//            address("localhost:10423")
//            adminAddress("localhost:10424")
//        }
//        rpcUsers = [[ user: "user1", "password": "test", "permissions": ["ALL"]]]
//
//    }
//    node {
//        name "O=NewYork,L=New York,C=US"
//        p2pPort 10405
//        rpcSettings {
//            address("localhost:10425")
//            adminAddress("localhost:10426")
//        }
//        rpcUsers = [[ user: "user1", "password": "test", "permissions": ["ALL"]]]
//
//    }
    node {
        name "O=CapeTownCustomer,L=Cape Town,C=ZA"
        p2pPort 10406
        rpcSettings {
            address("localhost:10427")
            adminAddress("localhost:10428")
        }
        rpcUsers = [[ user: "user1", "password": "test", "permissions": ["ALL"]]]

    }
    node {
        name "O=Regulator,L=Johannesburg,C=ZA"
        p2pPort 10407
        rpcSettings {
            address("localhost:10429")
            adminAddress("localhost:10430")
        }
        rpcUsers = [[ user: "user1", "password": "test", "permissions": ["ALL"]]]
    }




}

task installQuasar(type: Copy) {
    destinationDir rootProject.file("lib")
    from(configurations.quasar) {
        rename 'quasar-core(.*).jar', 'quasar.jar'
    }
}
